% Legend:
% "/\" = AND
% "\/" = OR
% "~"  = NOT
% "=>" = IMPLIES
% "%"  = COMMENT
%
% Graph node = (instruction, (pipeline, stage number))
% Graph edge = (node, node, label)
%
% "c" is predefined to be the core ID

StageName 0 "Fetch".
StageName 1 "Decode".
StageName 2 "Execute".
StageName 3 "MemoryStage".
StageName 4 "Writeback".
StageName 5 "StoreBuffer".
StageName 6 "ViCLCreate".
StageName 7 "ViCLExpire".
StageName 8 "MemoryHierarchy".
StageName 9 "Complete".

% BEGIN MACROS 

% NEW: From SpectreMeltdown.als
DefineMacro "ReadViCLsExist":
    (EdgeExists((i, ViCLCreate), (i, Execute), "")
    /\ EdgeExists((i, Execute), (i, ViCLExpire), "")
    /\ NodesExist [(i, ViCLCreate); (i, ViCLExpire)]).

% NEW: From SpectreMeltdown.als
% Not sure if i' is the correct thing to use
% if we remove i' from this then we get a shadowing error
% is this exists or forall?
DefineMacro "NoReadViCLs":
    (
      ~NodesExist [(i, ViCLCreate); (i, ViCLExpire)]
    ).

% NEW: Edited from SpectreMeltdown.als
% Not sure if we need to delete some of the predicates
DefineMacro "STBFwd":
exists microop "w",
exists microop "i", (
  SameThread w i
  /\ ProgramOrder w i
  % FIX /\ ExpandMacro NoReadViCLs
  /\ IsAnyWrite w 
  /\ SameVirtualAddress w i 
  /\ SameCore w i 
  /\ SameData w i 
  /\ EdgeExists ((w, (0,Execute)), (i, Execute), "")
  /\
  (
    (
      NodesExist [(w, Writeback)] /\ IsCacheable w /\ EdgeExists ((i, Execute), (w, ViCLCreate), "")
      /\ AddEdges [((w, MemoryStage), (i,  MemoryStage), "STBFwd", "red");
                  ((i, MemoryStage), (w, (0, ViCLCreate)), "STBFwd", "purple")]
    )
    \/
    (
      NodesExist [(w, Writeback)] /\ ~IsCacheable w /\ EdgeExists ((i, Execute), (w, MemoryHierarchy), "")
      /\ AddEdges [((w, MemoryStage), (i,  MemoryStage), "STBFwd", "red");
                  ((i, MemoryStage), (w, (0, MemoryHierarchy)), "STBFwd", "purple")]
    )
  )
  /\ ~exists microop "w'",(
  IsAnyWrite w' /\ SameVirtualAddress w w' /\ SameCore w w' /\
  ProgramOrder w w' /\ ProgramOrder w' i)
).

% NEW: Edited from SpectreMeltdown.als
% true if all stores before read have been written to the L1 Cache (Cacheable) 
% or Main Memory (NonCacheable)
DefineMacro "STBEmpty":
exists microop "i",
forall microop "w", (
  IsAnyWrite w => SameCore w i => SameVirtualAddress w i => ProgramOrder w i =>
  (
    (IsCacheable w /\ AddEdge ((w, (0, ViCLCreate)), (i, Execute), "STBEmpty", "purple"))
    \/
    (~IsCacheable w /\ AddEdge ((w, (0, MemoryHierarchy)), (i, Execute), "STBEmpty", "purple"))
  )
).

% NEW: Added from SpectreMeltdown.als
DefineMacro "ReadSourcedFromL1":
  exists microop "w", (
    SameCore w i
    % FIX /\ ExpandMacro NoReadViCLs
    /\ IsCacheable r
    /\ SameVirtualAddress w i
    /\ NodesExist [(w, Writeback)]
    /\ (
      (
        IsCacheable w
        /\ EdgesExist [((w, ViCLCreate), (i, Execute), "");
                       ((r, Execute), (w, ViCLExpire), "")]
      )
      \/
      (
        exists microop "i''",
        (
          SameCore i i''
          /\ NodesExist[(i'', ViCLCreate)]
          /\ EdgesExist [((i'', ViCLCreate), (i, Execute), "");
                         ((i, Execute), (i'', ViCLExpire), "")]
          /\
          (
            (
              IsCacheable w
              /\ (EdgesExist [((w, ViCLExpire), (i'', ViCLCreate), "");
                           ((w, MemoryHierarchy), (i'', ViCLCreate), "")])
            )
            \/
            (
              ~IsCacheable w
              /\
              EdgeExists ((w, MemoryHierarchy), (i'', ViCLCreate), "")
            )
          )
        )
      )
    )
  ).

% NEW: Added from SpectreMeltdown.als
% true if read is sourced from a same-core write from Main Memory
DefineMacro "InternalReadSourcedFromMM":
exists microop "w",
exists microop "i", (
  SameCore w i
  /\ NodesExist [(w, Commit)]
  /\
  (
    (
      IsCacheable i
      % FIX /\ ExpandMacro ReadViCLsExist
      /\ EdgeExists ((w, MemoryHierarchy), (i, ViCLCreate), "")
      /\
      (
        (
          IsCacheable w
          /\ EdgeExists ((w, ViCLExpire), (i, ViCLCreate), "")
        )
        \/
        ~IsCacheable w
      )
    )
    \/
    (
      ~IsCacheable i /\ EdgeExists ((w, MemoryHierarchy), (i, Execute), "")
    )
  )
).

% NEW: Added from SpectreMeltdown.als
% true if read is rouced from a different-core write from Main Memory
DefineMacro "ExternalReadSourcedFromMM":
exists microop "w",
exists microop "i", (
  ~SameCore w i
  /\ NodesExist [(w, Writeback)]
  /\
  (
    (
      IsCacheable i
      % FIX /\ ExpandMacro ReadViCLsExist
      /\ EdgeExists ((w, MemoryHierarchy), (i, ViCLCreate), "")
      /\
      (
        (IsCacheable w
        /\ EdgeExists ((w, ViCLExpire), (r, ViCLCreate), ""))
        \/
        ~IsCacheable w
      )
    )
    \/
    (
      ~IsCacheable r /\ EdgeExists ((w, MemoryHierarchy), (i, Execute), "")
    )
  )
).

% NEW: Added from SpectreMeltdown.als
DefineMacro "SameCoreCoherence":
exists microop "w",
exists microop "w'",(
  SameCore w w'
  /\ NodesExist [(w, Writeback); (w', Writeback)]
  /\
  (
    IsCacheable w /\ IsCacheable w'
    /\ EdgesExist [((w, ViCLCreate), (w', ViCLCreate), "");
                   ((w, ViCLExpire), (w', ViCLCreate), "");
                   ((w, MemoryHierarchy), (w', MemoryHierarchy), "")]
  )
  \/
  (
    ~IsCacheable w /\ ~IsCacheable w'
    /\ EdgeExists ((w, MemoryHierarchy), (w', MemoryHierarchy), "")
  )
).

% NEW: Added From SpectreMeltdown.als
DefineMacro "DifferentCoreCoherence":
exists microop "w",
exists microop "w'", (
  ~SameCore w w' /\ NodesExist [(w, Writeback); (w', Writeback)]
  /\ EdgeExists ((w, MemoryHierarchy), (w', MemoryHierarchy), "")
).

% NEW: Shared Memory Predicates
% NEW: Added from SpectreMeltdown.als
% We require the shared_ro_mem because Flush+Reload requires
% both processes to share 

DefineMacro "ReadFromLatestValue":
exists microop "w", (
  IsAnyWrite w /\ SamePhysicalAddress w i /\ SameData w i /\
  AddEdge ((w, (0, MemoryHierarchy)), (i,     MemoryStage ), "rf", "red") /\
  ~(exists microop "w'",
    IsWrite w' /\
    SamePhysicalAddress i w' /\
    EdgesExist [((w , (0, MemoryHierarchy)), (w', (0, MemoryHierarchy)), "");
                ((w', (0, MemoryHierarchy)), (i ,     MemoryStage     ), "")])).

DefineMacro "BeforeOrAfterEveryWriteToSamePA":
  % Either before or after every write to the same physical address
  forall microop "w", (
    (IsAnyWrite w /\ SamePhysicalAddress w i) =>
    (AddEdge ((w, (0, MemoryHierarchy)), (i,     MemoryStage     ), "wsrf", "darkred") \/
     AddEdge ((i,     MemoryStage     ), (w, (0, MemoryHierarchy)), "fr"  , "red"))).

DefineMacro "BeforeAllWrites":
  % Read occurs before all writes to same PA & Data
  DataFromInitialStateAtPA i /\
  forall microop "w", (
    IsAnyWrite w => SamePhysicalAddress w i => ~SameMicroop i w =>
    AddEdge ((i, MemoryStage), (w, (0, MemoryHierarchy)), "fr", "red")).

% BEGIN AXIOMS

Axiom "WriteIsBeforeFinal":
  forall microop "w",
  OnCore c w => (
    forall microop "w'",
    IsAnyWrite w => IsAnyWrite w' => SamePhysicalAddress w w' =>
       ~SameMicroop w w' => DataFromFinalStateAtPA w' =>
    AddEdge ((w, (0, MemoryHierarchy)), (w', (0, MemoryHierarchy)), "ws_final", "red")).

Axiom "WriteSerialization":
SameCore 0 c =>
forall microops "i1", forall microops "i2",
(~SameMicroop i1 i2) => IsAnyWrite i1 => IsAnyWrite i2 => SamePhysicalAddress i1 i2 => (
(AddEdge ((i1, (0, MemoryHierarchy)), (i2, (0, MemoryHierarchy)), "ws", "red")) \/
(AddEdge ((i2, (0, MemoryHierarchy)), (i1, (0, MemoryHierarchy)), "ws", "red"))).

Axiom "Reads":
forall microops "i",
OnCore c i =>
IsAnyRead i =>
AddEdges [((i, Fetch),       (i, Decode),      "path");
          ((i, Decode),      (i, Execute),     "path");
          ((i, Execute),     (i, MemoryStage), "path");
          ((i, MemoryStage), (i, Writeback),   "path")]
/\
(
  KnownData i
  =>
  (
    % FIX ExpandMacro STBFwd
    % FIX \/
    (
      ExpandMacro STBEmpty /\
      ExpandMacro BeforeOrAfterEveryWriteToSamePA /\
      (ExpandMacro BeforeAllWrites \/ ExpandMacro ReadFromLatestValue)
    )
  )
).

Axiom "Writes":
forall microops "i",
OnCore c i =>
IsAnyWrite i =>
AddEdges [((i, Fetch      ), (i,     Decode          ), "path");
          ((i, Decode     ), (i,     Execute         ), "path");
          ((i, Execute    ), (i,     MemoryStage     ), "path");
          ((i, MemoryStage), (i,     Writeback       ), "path");
          ((i, Writeback  ), (i,     StoreBuffer     ), "path");
          ((i, StoreBuffer), (i, (0, MemoryHierarchy)), "path")].

Axiom "mfence":
forall microops "f",
OnCore c f =>
IsAnyFence f =>
AddEdges [((f, Fetch       ), (f,     Decode      ), "path");
          ((f, Decode      ), (f,     Execute     ), "path");
          ((f, Execute     ), (f,     MemoryStage ), "path");
          ((f, MemoryStage ), (f,     Writeback   ), "path")]
/\ (
  forall microops "w",
  ((IsAnyWrite w /\ SameCore w f /\ ProgramOrder w f) =>
     AddEdge ((w, (0, MemoryHierarchy)), (f,     Execute ), "mfence", "orange"))).

Axiom "RMW":
forall microop "w",
IsAnyWrite w => AccessType RMW w =>
(forall microops "i2", ProgramOrder w i2 => IsAnyRead i2 =>
  AddEdge ((w, (0, MemoryHierarchy)), (i2, MemoryStage), "rmw", "orange")) /\
(exists microop "r", ConsecutiveMicroops r w /\ IsAnyRead r /\ AccessType RMW r /\
  ~exists microop "w'", IsAnyWrite w' /\ SamePhysicalAddress w w' /\
    EdgesExist [((r ,     MemoryStage     ), (w', (0, MemoryHierarchy)), "");
                ((w', (0, MemoryHierarchy)), (w , (0, MemoryHierarchy)), "")]).

Axiom "PO/Fetch":
forall microops "i1",
forall microops "i2",
(OnCore c i1 /\ OnCore c i2 /\ ProgramOrder i1 i2) =>
AddEdge ((i1, Fetch), (i2, Fetch), "PO", "blue").

Axiom "Decode_stage_is_in-order":
forall microops "i1",
forall microops "i2",
OnCore c i1 => OnCore c i2 =>
EdgeExists ((i1, Fetch), (i2, Fetch), "") =>
NodesExist [(i1, Decode); (i2, Decode)] =>
AddEdge ((i1, Decode), (i2, Decode), "PPO", "darkgreen").

Axiom "Execute_stage_is_in-order":
forall microops "i1",
forall microops "i2",
OnCore c i1 => OnCore c i2 =>
EdgeExists ((i1, Decode), (i2, Decode), "") =>
NodesExist [(i1, Execute); (i2, Execute)] =>
AddEdge ((i1, Execute), (i2, Execute), "PPO", "darkgreen").

Axiom "Memory_stage_is_in-order":
forall microops "i1",
forall microops "i2",
OnCore c i1 => OnCore c i2 =>
EdgeExists ((i1, Execute), (i2, Execute), "") =>
NodesExist [(i1, MemoryStage); (i2, MemoryStage)] =>
AddEdge ((i1, MemoryStage), (i2, MemoryStage), "PPO", "darkgreen").

Axiom "Writeback_stage_is_in-order":
forall microops "i1",
forall microops "i2",
OnCore c i1 => OnCore c i2 =>
EdgeExists ((i1, MemoryStage), (i2, MemoryStage), "") =>
NodesExist [(i1, Writeback);  (i2, Writeback)] =>
AddEdge ((i1, Writeback), (i2, Writeback), "PPO", "darkgreen").

Axiom "STB_FIFO":
forall microops "i1",
forall microops "i2",
OnCore c i1 => OnCore c i2 =>
EdgeExists ((i1, Writeback), (i2, Writeback), "") =>
NodesExist [(i1, StoreBuffer); (i2, StoreBuffer)] =>
AddEdge ((i1, StoreBuffer), (i2, StoreBuffer), "PPO", "darkgreen").

% NEW: edited to be same as SpectreMeltdown.als
Axiom "STB_OneAtATime":
forall microops "i1",
forall microops "i2",
OnCore c i1 => OnCore c i2 =>
EdgeExists ((i1, StoreBuffer), (i2, StoreBuffer), "") =>
(
  ~IsCacheable i1 /\ NodesExist [(i1, (0, MemoryHierarchy)); (i2, StoreBuffer)] =>
    AddEdge ((i1, (0, MemoryHierarchy)), (i2, StoreBuffer), "PPO", "darkgreen")
) \/ (
  IsCacheable i1 /\ NodesExist [(i1, (0, ViCLCreate)); (i2, StoreBuffer)]
  /\ ~EdgeExists ((i1, MemoryHierarchy), (i2, StoreBuffer), "") =>
    AddEdge ((i1, (0, ViCLCreate)), (i2, StoreBuffer), "PPO", "darkgreen")
).

% NEW: Added from SpectreMeltdown.als
Axiom "Complete_stage_is_in-order":
forall microops "i1",
forall microops "i2",
(OnCore c i1 /\ OnCore c i2 /\ ProgramOrder i1 i2) =>
EdgeExists ((i1, Fetch), (i2, Fetch), "") =>
AddEdge ((i1, Complete), (i2, Complete), "PO", "blue").

% NEW: Threading and process scheduling