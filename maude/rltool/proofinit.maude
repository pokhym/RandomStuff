--- file: proofinit.maude
--- reqs: prelude, full-maude, var-unif, var-sat, etc...
--- info: this file contains code to define the operations necessary
---       for doing reachablity proofs by hooking into backend function
---       implementations defined in other modules

fmod REACH-PROOF-INIT is
  pr REACH-PROOF-STATE-OPS .
  pr RLTOOL-BACKEND-IMPL .

  var U : Module . var RT : ProofStrat . var PS : ProofGoalStatus . var B : Bool . var N : Nat .

  var F : ReachForm . var TST' : QFCTermSet .
  var FS FS' : ReachFormSet . var LFS LFS' : LabelReachFormSet .
  var CRL CRL' AXL AXL' : LabelReachFormList . var ARS ARS' : LabelLCCRuleSet .
  var D : ProofMetadata .
  var GS : ProofGoalSet .
  var D1 D2 : NameData .
  var S : Substitution .

  --- PRE: Arguments are well-defined with respect to Module
  --- OUT: Initialize our proof state with goals from axioms
  ---      and terminating states; rename everything so that name
  ---      collisions won't happen between different pieces
  op init  : ProofMetadata -> ProofMetadata .
  eq init(D) =
    if get-prf(D) == mt then
        raw-set(D,st(err) ; out('Error: 'No 'goals 'specified))
    else if get-mod(D) == noModule then
        raw-set(D,st(err) ; out('Error: 'No 'module 'loaded))
    else if get-reg(D) == empty then
        raw-set(D,st(err) ; out('Error: 'No 'validity/satisfiability 'checking 'backend 'is 'loaded))
    else if get-tst(D) == noterm then
        raw-set(D,st(err) ; out('Terminating 'state 'set 'undefined))
    else
        init(get-mod(D),D)
    fi fi fi fi .

  op init : Module ProofMetadata -> ProofMetadata .
 ceq init(U,D) = set(D,on(true) ; rls(ARS') ; tst(TST') ; axs(AXL') ;  prf(GS) ;  cnt(N) ; gn(1 + size(GS)))
  if [D1,   ARS'] := renameVarsLS(nullNameData,abstract-rules(U,abstractRules)) --- abstracted rewrite rules
  /\ [D2,   TST'] := #renameVars(D1,get-tst(D))                                 --- renamed final state pattern
  /\ [[N,S],AXL'] := renameVarsLL(D2,get-axs(D))                                --- renamed axioms
  /\ GS           := renameNormalizeGoal(U,N,get-prf(D)) .                     --- renamed goals

  eq init(U,D) = raw-set(D,st(err) ; out('Internal 'Error: 'init' 'failed)) [owise] .
endfm
